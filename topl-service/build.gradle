apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: rootProject.file('gradle/maven-publish.gradle')

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments disableAnalytics: 'true'
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += [
                '-Xopt-in=kotlin.time.ExperimentalTime',
                '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
        ]
    }
}

dokka {
    configuration {
        moduleName = 'topl-service'
        subProjects = [ 'topl-core-base', 'topl-service-base', 'topl-core' ]
        reportUndocumented = false
        includeNonPublic = false
        skipEmptyPackages = true
        samples = [
                "$rootDir/sampleapp/src/main/java/io/matthewnelson/sampleapp/topl_android/CodeSamples.kt".toString()
        ]
        sourceLink {
            url = "https://github.com/05nelsonm/TorOnionProxyLibrary-Android/blob/master/"
            lineSuffix = "#L"
        }
        externalDocumentationLink {
            // Dokka plugin requires a protocol (http/https/etc.) here, so we have to repair the
            // links after building the docs by running `./gradlew fixDokkaDocLinks`.
            url = new URL("http://FIX_DOKKA_LINKS/topl-service-base/")
            packageListUrl = new URL("file://$rootDir/docs/topl-service-base/package-list")
        }
        externalDocumentationLink {
            // Dokka plugin requires a protocol (http/https/etc.) here, so we have to repair the
            // links after building the docs by running `./gradlew fixDokkaDocLinks`.
            url = new URL("http://FIX_DOKKA_LINKS/topl-core/")
            packageListUrl = new URL("file://$rootDir/docs/topl-core/package-list")
        }
        externalDocumentationLink {
            // Dokka plugin requires a protocol (http/https/etc.) here, so we have to repair the
            // links after building the docs by running `./gradlew fixDokkaDocLinks`.
            url = new URL("http://FIX_DOKKA_LINKS/topl-core-base/")
            packageListUrl = new URL("file://$rootDir/docs/topl-core-base/package-list")
        }
    }
    outputFormat = 'gfm'
    outputDirectory = "$rootDir/docs/"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':topl-core')
    api project(path: ':topl-service-base')
    implementation deps.androidx.core
    implementation deps.androidx.lifecycle.runtime
    implementation deps.androidx.lifecycle.processLifecycleOwner
    implementation deps.jtorctl
    implementation deps.kotlin.coroutinesAndroid
    implementation deps.kotlin.coroutinesCore
    implementation deps.kotlin.stdlib

    testImplementation deps.androidx.test.core
    testImplementation deps.junit
    testImplementation deps.kotlin.coroutinesTest
    testImplementation deps.robolectric

    androidTestImplementation deps.androidx.test.core
    androidTestImplementation deps.androidx.test.espresso
    androidTestImplementation deps.androidx.test.junit
}